namespace Myriad
open System
open Fantomas
open System.IO
open Microsoft.FSharp.Compiler.Ast
open FsAst
open Argu

module Main =

    type Arguments =
        | [<Mandatory>] InputFile of string
        | Namespace of string
        | [<Mandatory>] OutputFile of string
    with
        interface IArgParserTemplate with
            member s.Usage =
                match s with
                | InputFile _ -> "specify a file to use as input."
                | Namespace _ -> "specify a namespace to use."
                | OutputFile _ -> "Specify the file name that the generated code will be written to."

    [<EntryPoint>]
    let main argv =
        let parser = ArgumentParser.Create<Arguments>(programName = "myriad")

        try
            let results = parser.Parse argv
            let inputFile = results.GetResult InputFile
            let outputFile = results.GetResult OutputFile
            let namespace' =
                match results.TryGetResult Namespace with
                | Some ns -> ns
                | None -> Path.GetFileNameWithoutExtension(inputFile)

            let ast = Ast.getAst inputFile
            let records = Ast.extractRecordMeta ast
            let modules = 
                records
                |> List.map (fun (p,fs) -> Ast.createRecordModule p fs )
    
            let namespace' = 
                {SynModuleOrNamespaceRcd.CreateNamespace(Ident.CreateLong namespace')
                    with    
                        IsRecursive = true
                        Declarations = modules }

            let parseTree =
                ParsedInput.CreateImplFile(
                    ParsedImplFileInputRcd.CreateFs(outputFile)
                        .AddModule(namespace'))

            let formattedCode = formatAst parseTree

            let code =
                [   "//------------------------------------------------------------------------------"
                    "//        This code was generated by myriad."
                    "//        Changes to this file will be lost when the code is regenerated."
                    "//------------------------------------------------------------------------------"
                    formattedCode ]
                |> String.concat (Environment.NewLine)

            File.WriteAllText(outputFile, code)

            printfn "%A" code
#if DEBUG
            printfn "AST-----------------------------------------------"
            printfn "%A" parseTree
#endif
            0 // return an integer exit code

        with
        | :? ArguParseException as ae ->
            printfn "%s" ae.Message
            match ae.ErrorCode with
            | Argu.ErrorCode.HelpText -> 0
            | _ -> 2
        | :? ArguParseException as ae when ae.ErrorCode = Argu.ErrorCode.HelpText ->
            printfn "%s" ae.Message
            3
        | :? FileNotFoundException as fnf ->
            printfn "ERROR: inputfile %s doesn not exist\n%s" fnf.FileName (parser.PrintUsage())
            4